#!/usr/bin/env python3

from basecall_wrapper.__main__ import sort_fastq_by_readlength
from basecall_wrapper.__main__ import get_full_path
import re
import shutil
import pathlib2


###########
# GLOBALS #
###########

# from config
outdir = config['outdir']
raw_data = config['raw_data']
log_dir = config['log_dir']
flowcell = config['flowcell']
kit = config['kit']

# intermediate files
unzip_folder = os.path.join(outdir, 'unzipped_raw_data')
unzip_tmpfile = os.path.join(unzip_folder, 'tmp_file')
basecall_folder = os.path.join(outdir, 'basecalled')
merged_folder = os.path.join(outdir, 'merged')
temp_fq = os.path.join(merged_folder, 'tmp.fq')
merged_fq = os.path.join(merged_folder, 'merged.fq')
sorted_fq = os.path.join(merged_folder, 'merged_sorted.fq')
final_fq = os.path.join(merged_folder, 'merged_sorted.fq.gz')
stats_dir = os.path.join(outdir, 'stats')
if not os.path.isdir(stats_dir):
    os.makedirs(stats_dir)
scripts_dir = os.path.join(outdir, 'scripts')

# samples to get results for
sample_names = [re.sub('\..+$', '', str(pathlib2.Path(x).name))
                for x in raw_data]

#########
# RULES #
#########

# master rule
rule all:
    input:
        # final_fq,
        # os.path.join(stats_dir, 'length_histogram.pdf'),
        os.path.join(stats_dir, 'minionqc'),
        final_fq
        # expand('{}/{{sample_name}}'.format(basecall_folder),
        #        sample_name=sample_names)

# setup scripts
rule setup_scripts:
    input:
        shutil.which('plot_histogram.R')
    output:
        temp(os.path.join(scripts_dir, 'plot_histogram.R'))
    shell:
        'cp {input} {output}'


# unzip raw data to temp folder
rule mv_raw_data:
    input:
        raw_data
    output:
        temp(expand('{}/{{sample_name}}.tar.gz'.format(unzip_folder),
                    sample_name=sample_names))
    params:
        folder = unzip_folder
    shell:
        'mkdir -p {params.folder} || exit 1 ; '
        'cp {input} {params.folder}'

rule unzip:
    input:
        '{}/{{sample_name}}.tar.gz'.format(unzip_folder)
    output:
        temp(directory('{}/{{sample_name}}'.format(unzip_folder)))
    log:
        os.path.join(log_dir, 'unzip_{sample_name}.log')
    shell:
        'mkdir -p {output} || exit 1 ; '
        'tar -zxf {input} -C {output} --strip-components 1 &> {log}'

# basecall
rule basecall:
    input:
        '{}/{{sample_name}}'.format(unzip_folder)
    output:
        directory('{}/{{sample_name}}'.format(basecall_folder))
    params:
        kit = kit,
        flowcell = flowcell,
        folder = basecall_folder
    threads:
        config['threads']
    shell:
        # 'mkdir -p {params.folder} || exit 1 ; '
        'read_fast5_basecaller.py '
        '--worker_threads {threads} '
        '--input {input} '
        '--save_path {output} '
        '--flowcell {params.flowcell} '
        '--kit {params.kit} '
        '--recursive '
        '--output_format fastq '

# merge
rule merge_per_sample:
    input:
        '{}/{{sample_name}}'.format(basecall_folder)
    output:
        temp('{}/{{sample_name}}.fastq'.format(merged_folder))
    shell:
        'cat {input}/workspace/pass/*.fastq | '
        'sed -e \'/^[[:space:]]*$/d\' > '
        '{output}'

rule merge:
    input:
        expand('{}/{{sample_name}}.fastq'.format(merged_folder),
               sample_name=sample_names)
    output:
        temp(temp_fq)
    shell:
        'cat {input} > {output}'

# reformat
rule reformat:
    input:
        temp_fq
    output:
        fq = temp(merged_fq),
        lhist = os.path.join(stats_dir, 'lhist.txt')
    params:
        memory = config['memory']
    log:
        os.path.join(log_dir, 'reformat.log')
    shell:
        'reformat.sh '
        '-Xmx{params.memory}g '
        'in={input} '
        'interleaved=f '
        'out={output.fq} '
        'ow=t '
        'bhist={stats_dir}/bhist.txt '
        'qhist={stats_dir}/qhist.txt '
        'qchist={stats_dir}/qchist.txt '
        'aqhist={stats_dir}/aqhist.txt '
        'bqhist={stats_dir}/bqhist.txt '
        'lhist={output.lhist} '
        'gchist={stats_dir}/gchist.txt '
        'gcbins=auto '
        '2> {log}'

# sort
rule sort:
    input:
        fq = merged_fq
    output:
        fq = temp(sorted_fq)
    run:
        sort_fastq_by_readlength(input.fq, output.fq)

# gzip
rule compress:
    input:
        sorted_fq
    output:
        final_fq
    log:
        os.path.join(log_dir, 'compress.log')
    shell:
        'gzip --best --to-stdout --verbose '
        '{input} > {output} 2> {log}'

# plot histogram
rule histogram:
    input:
        lhist = os.path.join(stats_dir, 'lhist.txt'),
        fq = final_fq,
        script = os.path.join(scripts_dir, 'plot_histogram.R')
    output:
        plot = os.path.join(stats_dir, 'length_histogram.pdf')
    script:
        str(pathlib2.Path(scripts_dir, 'plot_histogram.R').resolve())

# qc script
rule minion_qc:
    input:
        expand('{}/{{sample_name}}'.format(basecall_folder),
               sample_name=sample_names)
    output:
        directory(os.path.join(stats_dir, 'minionqc'))
    params:
        basecall_folder = basecall_folder
    log:
        os.path.join(log_dir, 'minion_qc.log')
    threads:
        len(sample_names)
    priority:
        1
    shell:
        'MinIONQC.R '
        '-i {params.basecall_folder} '
        '-o {output} '
        '-p {threads} '
        '&> {log}'
