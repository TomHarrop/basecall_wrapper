#!/usr/bin/env python3

from basecall_wrapper.__main__ import sort_fastq_by_readlength
from basecall_wrapper.__main__ import get_full_path


###########
# GLOBALS #
###########

# from config
outdir = config['outdir']
raw_data = config['raw_data']
log_dir = config['log_dir']
flowcell = config['flowcell']
kit = config['kit']

print(raw_data)

# intermediate files
unzip_folder = os.path.join(outdir, 'unzipped_raw_data')
unzip_tmpfile = os.path.join(unzip_folder, 'tmp_file')
basecall_folder = os.path.join(outdir, 'basecalled')
basecall_tempfile = os.path.join(basecall_folder, 'tmp_file')
merged_folder = os.path.join(outdir, 'merged')
temp_fq = os.path.join(merged_folder, 'tmp.fq')
merged_fq = os.path.join(merged_folder, 'merged.fq')
sorted_fq = os.path.join(merged_folder, 'merged_sorted.fq')
final_fq = os.path.join(merged_folder, 'merged_sorted.fq.gz')
stats_dir = os.path.join(outdir, 'stats')
if not os.path.isdir(stats_dir):
    os.makedirs(stats_dir)

# scripts
plot_histogram = get_full_path('plot_histogram.R')

#########
# RULES #
#########

# master rule
rule all:
    input:
        final_fq,
        os.path.join(stats_dir, 'length_histogram.pdf')      

# unzip raw data to temp folder
rule unzip:
    input:
        raw_data
    output:
        temp(unzip_folder)
    params:
        folder = unzip_folder
    log:
        os.path.join(log_dir, 'unzip.log')
    shell:
        'mkdir -p {params.folder} || exit 1 ; '
        'cat {input} | tar zxvf - -C {params.folder} &> {log}'
#        'tar xvf {input} -C {output.folder} &> {log}'

# basecall
rule basecall:
    input:
        unzip_folder
    output:
        basecall_folder
    params:
        kit = kit,
        flowcell = flowcell,
        folder = basecall_folder
    threads:
        config['threads']
    shell:
        'mkdir -p {params.folder} || exit 1 ; '
        'read_fast5_basecaller.py '
        '--worker_threads {threads} '
        '--input {input} '
        '--save_path {params.folder} '
        '--flowcell {params.flowcell} '
        '--kit {params.kit} '
        '--recursive '
        '--output_format fastq '

# merge
rule merge:
    input:
        basecall_folder
    output:
        temp(temp_fq)
    shell:
        'cat {input}/workspace/pass/*.fastq | '
        'sed -e \'/^[[:space:]]*$/d\' > '
        '{output}'

# reformat
rule reformat:
    input:
        temp_fq
    output:
        fq = temp(merged_fq),
        lhist = os.path.join(stats_dir, 'lhist.txt')
    params:
        memory = config['memory']
    log:
        os.path.join(log_dir, 'reformat.log')
    shell:
        'reformat.sh '
        '-Xmx{params.memory}g '
        'in={input} '
        'interleaved=f '
        'out={output.fq} '
        'ow=t '
        'bhist={stats_dir}/bhist.txt '
        'qhist={stats_dir}/qhist.txt '
        'qchist={stats_dir}/qchist.txt '
        'aqhist={stats_dir}/aqhist.txt '
        'bqhist={stats_dir}/bqhist.txt '
        'lhist={output.lhist} '
        'gchist={stats_dir}/gchist.txt '
        'gcbins=auto '
        '2> {log}'

# sort
rule sort:
    input:
        fq = merged_fq
    output:
        fq = temp(sorted_fq)
    run:
        sort_fastq_by_readlength(input.fq, output.fq)

# gzip
rule compress:
    input:
        sorted_fq
    output:
        final_fq
    log:
        os.path.join(log_dir, 'compress.log')
    shell:
        'gzip --best --to-stdout --verbose '
        '{input} > {output} 2> {log}'

# plot histogram
rule histogram:
    input:
        lhist = os.path.join(stats_dir, 'lhist.txt'),
        fq = final_fq
    output:
        plot = os.path.join(stats_dir, 'length_histogram.pdf')
    script:
        '{}'.format(plot_histogram)
